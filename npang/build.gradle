buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}


ext.androidAnnotationsVersion = '2.7.1';

configurations {
    apt
}

def getSourceSetName(variant) {
    return new File(variant.dirName).getName();
}

android {
    compileSdkVersion 18
    buildToolsVersion "19.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    applicationVariants.all { variant ->
        def aptOutputDir = project.file("${project.buildDir}/source/apt_generated")
        def aptOutput = new File(aptOutputDir, variant.dirName)

        println "****************************"
        println "variant: ${variant.name}"
        println "manifest:  ${variant.processResources.manifestFile}"
        println "aptOutput:  ${aptOutput}"
        println "confi ${configurations.apt.getAsPath()}"
        println "mani  ${variant.processResources.manifestFile}"
        println "****************************"

        sourceSets[getSourceSetName(variant)].java.srcDirs += aptOutput.getPath()

        variant.javaCompile.source = variant.javaCompile.source.filter { p ->
            return !p.getPath().startsWith(aptOutputDir.getPath())
        }

        variant.javaCompile.doFirst {
            println "*** compile doFirst ${variant.name}"
            aptOutput.mkdirs()
            variant.javaCompile.options.compilerArgs += [
                    '-processorpath', configurations.apt.getAsPath(),
                    '-AandroidManifestFile=' + variant.processResources.manifestFile,
                    '-s', aptOutput
            ]
        }
    }
}

dependencies {
    apt "com.googlecode.androidannotations:androidannotations:${androidAnnotationsVersion}"
    compile "com.googlecode.androidannotations:androidannotations-api:${androidAnnotationsVersion}"

    compile 'com.android.support:support-v4:19.0.0'
    compile 'ch.acra:acra:4.5.0'
    compile 'de.greenrobot:eventbus:2.2.0'
    compile 'com.j256.ormlite:ormlite-android:4.47'
    compile 'com.koushikdutta.urlimageviewhelper:urlimageviewhelper:1.0.4'
    compile 'com.nhaarman.listviewanimations:library:2.5.2'

    compile project(':libs:AutoScaleTextView')
    compile project(':libs:AndroidBootstrap')
    compile project(':libs:AndroidHorizontalListView')
    compile project(':libs:CurrencyEditText')
}



